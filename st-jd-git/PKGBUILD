# Maintainer: Jonas Dujava <jonas.dujava@gmail.com>

pkgname='st-jd-git'
_pkgname='st'
pkgver=0.9.r50.922f521
pkgrel=2
pkgdesc='A simple virtual terminal emulator for X - custom build'
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
_repo_prefix='github.com/jdujava'
_repo_name="${pkgname%-git}"
url="https://${_repo_prefix}/${_repo_name}"
source=("${_repo_name}::git+https://${_repo_prefix}/${_repo_name}" "st-terminfo.hook")
license=('MIT')
depends=('sh' 'glibc' 'libx11' 'fontconfig' 'freetype2' 'libxrender' 'libxft')
optdepends=(
    'dmenu: URL selection'
    'xsel: clipboard integration'
    'xorg-xrdb: Xresources support'
)
makedepends=('git' 'make')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
sha256sums=('SKIP' 'f5003599b82ad802744d2075e1020d6221f6f57c5d2dbbe29f7d8ced1358aa27')

pkgver() {
    cd "$srcdir/${_repo_name}"
    (
        set -o pipefail
        git describe --long --tags 2>/dev/null | sed "s/^[A-Za-z\.\-]*//;s/\([^-]*-\)g/r\1/;s/-/./g" ||
            printf "r%s.%s\n" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

prepare() {
    cd "$srcdir/${_repo_name}"
    # to use a custom config.h, place it in the package directory
    if [ -f "$srcdir/config.h" ]; then
        cp -fv "$srcdir/config.h" config.h
    fi
}

build() {
    cd "$srcdir/${_repo_name}"
    make
}

package() {
    cd "$srcdir"
    install -m644 -D st-terminfo.hook "$pkgdir/usr/share/libalpm/hooks/st-terminfo.hook"
    cd "$srcdir/${_repo_name}"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -m644 -D README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
    install -m644 -D st.info "$pkgdir/usr/share/$pkgname/st.info"
    sudo tic -sx st.info
}
