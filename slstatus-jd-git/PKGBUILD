# Maintainer: Jonas Dujava <jonas.dujava@gmail.com>

pkgname='slstatus-jd-git'
_pkgname='slstatus'
pkgver=1.0.r12.64427e5
pkgrel=2
pkgdesc='A status monitor for window managers - custom build'
arch=('i686' 'x86_64')
_repo_prefix='github.com/jdujava'
_repo_name="${pkgname%-git}"
url="https://${_repo_prefix}/${_repo_name}"
source=("${_repo_name}::git+https://${_repo_prefix}/${_repo_name}")
license=('ISC')
depends=('sh' 'glibc' 'libx11' 'libmpdclient' 'alsa-lib')
optdepends=(
    'pamixer: volume indicator'
    'dunst: notification indicator'
  )
makedepends=('git' 'make')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/${_repo_name}"
    (
        set -o pipefail
        git describe --long --tags 2>/dev/null | sed "s/^[A-Za-z\.\-]*//;s/\([^-]*-\)g/r\1/;s/-/./g" ||
            printf "r%s.%s\n" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

prepare() {
    cd "$srcdir/${_repo_name}"
    echo "CPPFLAGS+=${CPPFLAGS}" >> config.mk
    echo "CFLAGS+=${CFLAGS}" >> config.mk
    echo "LDFLAGS+=${LDFLAGS}" >> config.mk
    # to use a custom config.h, place it in the package directory
    if [ -f "$srcdir/config.h" ]; then
        cp -fv "$srcdir/config.h" config.h
    fi
}

build() {
    cd "$srcdir/${_repo_name}"
    make
}

package() {
    cd "$srcdir/${_repo_name}"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -m644 -D README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
